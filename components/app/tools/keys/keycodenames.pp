{
   keycodenames, creates a list of keycode names
   Copyright (c) 2009. Dejan Boras

      This program will create a keycode name list from the appKeyCodes.inc
   file. It is required to be done whenever appKeyCodes.inc is changed to make
   sure that keycode names list is valid.

   Started On:    02.10.2009.
}

{$MODE OBJFPC}{$H+}{$I-}
PROGRAM keycodenames;

USES
   StringUtils, ConsoleUtils,
   uSimpleParser;

TYPE
   TKeyCodeName = string[63];

   TKeyCode = record
      kcName: TKeyCodeName;
   end;

VAR
   kcnames: array[0..255] of TKeyCode;
   maxKeycodes: longint = 0;

function doReadFile(var s: string): boolean;
var
   item: string;
   value, code: longint;
   kcName: TKeyCodeName;

begin
   doReadFile := true;

   {check the string}
   if(pos('=', s) <> 0) and (pos(';', s) <> 0) then begin
      {get the keycode name}
      item := CopyToDel(s, '=');
      StripWhiteSpace(item);

      {check the keycode name}
      {NOTE: Keycode constant have a 'kc' prefix and have at least
      1 character(therefore the minimum size is 3 characters).}
      if(pos('kc', item) = 0) or (Length(item) < 3) then
         exit;

      kcName := item;

      {delete the equal symbol}
      delete(s, 1, 1);

      {get the keycode value}
      item := CopyToDel(s, ';');
      StripWhitespace(s);

      val(item, value, code);

      {add the keycode to the list}
      if(code = 0) and (value < 255) and (value > 0) then
         kcnames[value].kcName := kcName;
   end;
end;

procedure ProcessData();
var
   i, max: longint;

begin
   max := 0;

   for i := 0 to 255 do begin
      if(Length(kcnames[i].kcName) > 2) then
         max := i;
   end;

   maxKeycodes := max;
end;

function doWriteFile(var f: text): boolean;
var
   i: longint;
   s, ls: string;

begin
   doWritefile := true;

   {write a header}
   writeln(f, '{');
   writeln(f, '   File generated by keycodenames program from appKeyCodes.inc.');
   writeOnDate(f);
   writeln(f, '}');
   writeln(f, '');

   {write constants}
   writeln(f, '   appkcKeyCodeNames = '+sf(maxKeycodes + 1) + ';');
   writeln(f, '');

   {write keycode strings}
   writeln(f, '   {keycode name strings}');

   for i := 0 to maxKeycodes do begin
      if(Length(kcnames[i].kcName) > 0) then begin
         s := 'kcs' + copy(kcnames[i].kcName, 3, 255);

         ls := '   ' + s + ': string[' + sf(Length(kcnames[i].kcName)) + '] = ''' +
            kcnames[i].kcName + ''';';

         write(f, ls);
         s := sf(i);
         dStringAddPaddingLeading(s, '0', 3);
         writeln(f, ' {' + s + '}');
      end;
   end;

   writeln(f, '');

   {write the keycode name array, a list of pointers to strings}
   writeln(f, '   {keycode name array}');
   writeln(f, '   kcKeyCodeNames: array[0..appkcKeyCodeNames-1] of pshortstring = (');

   for i := 0 to maxKeycodes do begin
      s := sf(i);
      dStringAddPaddingLeading(s, '0', 3);
      write(f, '{'+s+'}');

      if(Length(kcnames[i].kcName) > 0) then begin
         s := '@kcs' + copy(kcnames[i].kcName, 3, 255);
         write(f, s);
      end else
         write(f, 'nil');

      if(i < maxKeycodes) then
         write(f, ',');

      writeln(f);
   end;

   writeln(f, ');');
end;

BEGIN
   writeln('keycodenames v1.0');
   writeln('This program is open source under GNU GPL v3.');
   writeln();

   {read the file}
   ReadFile('appKeyCodes.inc', @doReadFile);

   if(ioE <> 0) then
      exit;

   {process data}
   ProcessData();

   if(maxKeycodes = 0) then begin
      console.e('There are no keycodes.');
      exit;
   end;

   {write the file}
   WriteFile('appKeyCodeNames.inc', @doWriteFile);

   if(ioE <> 0)
      then exit;

   Writeln('Operation completed successfully.');
END.

